load("@aspect_rules_js//js:defs.bzl", "js_test")
load("@aspect_bazel_lib//lib:copy_to_bin.bzl", "copy_to_bin")
load("@npm//react-scripts:package_json.bzl", "bin")
load("@npm//:defs.bzl", "link_js_packages")

link_js_packages()

# Filename conventions described at
# https://create-react-app.dev/docs/running-tests#filename-conventions
_TESTS = [
    "src/**/*.test.js*",
    "src/**/*.test.ts*",
    "src/**/*.spec.js*",
    "src/**/*.spec.ts*",
    "src/**/__tests__/**/*.js*",
    "src/**/__tests__/**/*.ts*",
]

# So we copy everything it wants to read to the output "bin" directory
copy_to_bin(
    name = "static_files",
    srcs = glob(
        [
            "public/*",
            "src/**/*",
        ],
        exclude = _TESTS,
    ) + [
        "package.json",
        # "tsconfig.json",
    ],
)

_RUNTIME_DEPS = [
    "static_files",
    "@npm//react",
    "@npm//react-dom",
]

bin.react_scripts(
    # Note: If you want to change the name make sure you update BUILD_PATH below accordingly
    # https://create-react-app.dev/docs/advanced-configuration/
    name = "build",
    args = ["build"],
    srcs = _RUNTIME_DEPS + [
        "@npm//react-scripts",
        #"@npm//@types",
    ],
    env = {
        "BUILD_PATH": "./build",
    },
    output_dir = True,
)

js_test(
    name = "build_smoke_test",
    data = [
        "build",
        "@npm//@bazel/runfiles",
    ],
    entry_point = "build_smoke_test.js",
)

copy_to_bin(
    name = "copy_test_files",
    srcs = glob(_TESTS),
)

bin.react_scripts_test(
    name = "test",
    args = [
        "test",
        # ibazel is the watch mode for Bazel when running tests
        # Because Bazel is really a CI system that runs locally
        "--watchAll=false",
        "--no-cache",
        "--no-watchman",
        "--ci",
    ],
    data = _RUNTIME_DEPS + [
        "copy_test_files",
        "@npm//@testing-library/jest-dom",
        "@npm//@testing-library/react",
        "@npm//@testing-library/user-event",
    ],
)

bin.react_scripts_binary(
    name = "start",
    args = [
        "start",
    ],
    data = _RUNTIME_DEPS + [
        "@npm//@types/react",
        "@npm//@types/react-dom",
    ],
    tags = [
        # This tag instructs ibazel to pipe into stdin a event describing actions.
        # ibazel send EOF to stdin by default and `react-scripts start` will stop when getting EOF in stdin.
        # So use this to prevent EOF.
        "ibazel_notify_changes",
    ],
)
